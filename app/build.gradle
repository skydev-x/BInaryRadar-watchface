plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'org.jetbrains.kotlin.kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdk 34

  namespace "dev.dukendev.watchface.binaryradar"

  defaultConfig {
    applicationId "dev.dukendev.watchface.binaryradar"
    minSdk 31
    targetSdk 33
    versionCode 125
    versionName "1.4.5"
  }

  signingConfigs {
    config {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
    debug {
      storeFile rootProject.file('debug.keystore')
      keyAlias 'androiddebugkey'
      keyPassword 'android'
      storePassword 'android'
    }
  }

  buildFeatures {
    viewBinding true
  }

  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
    }
    release {
      debuggable false
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
      signingConfig signingConfigs.debug
    }
  }


  packagingOptions {
    exclude "META-INF/atomicfu.kotlin_module"
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.majorVersion
  }
}

dependencies {
  compileOnly libs.google.wearable
  implementation libs.play.services.wearable
  implementation libs.androidx.core.ktx

  // Kotlin components
  implementation libs.kotlinx.coroutines.core
  implementation libs.kotlinx.coroutines.android

  implementation libs.androidx.activity.ktx

  // Lifecycle components
  implementation libs.androidx.lifecycle.runtime.ktx
  implementation libs.androidx.lifecycle.viewmodel.ktx

  // Material components
  implementation libs.android.material

  // Wear libraries
  implementation libs.androidx.wear

  // Watch face specific libraries
  implementation libs.wear.watchface.complications.data
  implementation libs.wear.watchface.complications.data.source

  implementation libs.wear.watchface
  implementation libs.wear.watchface.client
  implementation libs.wear.watchface.complications.rendering
  implementation libs.wear.watchface.data
  implementation libs.wear.watchface.editor
  implementation libs.wear.watchface.style
}
